buildscript {
    ext.kotlin_version = '2.1.0'//'2.1.0'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {  
        classpath 'com.android.tools.build:gradle:8.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.4.2'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url = uri("https://storage.googleapis.com/r8-releases/raw")
        }
    }
}
rootProject.buildDir = "../build"
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    afterEvaluate {
        if (it.hasProperty('android')) {
            // Check whether the android.namespace property is set.
            // If not, read the package name from the app's manifest file and set it as the namespace.
            if (it.android.namespace == null) {
                def manifest = new XmlSlurper().parse(file(it.android.sourceSets.main.manifest.srcFile))
                def packageName = manifest.@package.text()
                println("Setting ${packageName} as android namespace")
                android.namespace = packageName
            }

            // Set both the sourceCompatibility and targetCompatibility to Java 17,
            // ensuring that both Java and Kotlin code are compiled with the correct target JVM version.
            def javaVersion = JavaVersion.VERSION_17
            android {
                def androidApiVersion = 35
                compileSdkVersion androidApiVersion
                defaultConfig {
                    targetSdkVersion androidApiVersion
                    // This block is different from the one you use to link Gradle
                    // to your CMake or ndk-build script.
                    externalNativeBuild {
                        // For ndk-build, instead use the ndkBuild block.
                        cmake {
                            // Passes optional arguments to CMake.
                            arguments += ["-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"]
                        }
                    }
                }
                compileOptions {
                    sourceCompatibility javaVersion
                    targetCompatibility javaVersion
                }
            }

            // Ensure that all subprojects in the build follow the same Java version
            // and build directory configurations. Evaluate settings after all configurations
            // have been applied to make sure everything is in sync.
            tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
                kotlinOptions {
                    jvmTarget = "17"
                }
            }
        }
    }
}
subprojects {
    project.evaluationDependsOn(":app")
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}